# Maintainer: John Ramsden <johnramsden [at] riseup [dot] net>

pkgname=python-pyzfscmds-git
pkgver=r27.4c3dd48
pkgrel=1
pkgdesc="ZFS CLI Function Wrapper"
arch=('any')
url="http://github.com/johnramsden/pyzfscmds"
license=('BSD' 'custom:BSD 3 clause')
depends=('zfs-linux' 'python' 'python-setuptools')
makedepends=('git' 'python-pip')
#optdepends=('systemdboot: Integration with bootloader')
source=('python-pyzfscmds::git+https://github.com/johnramsden/pyzfscmds#branch=master')
#validpgpkeys=('')
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${pkgname%-git}"
    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname%-git}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
}
